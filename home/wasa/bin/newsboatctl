#!/usr/bin/env chicken-scheme
;; -*- mode: scheme; -*-

(use files srfi-1 data-structures extras utils posix irregex
     scsh-process sql-de-lite)

(define (xdg-path environment-variable fallback path)
  (let ((home (get-environment-variable environment-variable)))
    (if (and home (eqv? (string-ref home 0) #\/))
        (string-append home path)
        (string-append (get-environment-variable "HOME") fallback path))))

(define urls-file (xdg-path "XDG_CONFIG_HOME" "/.config" "/newsboat/urls"))
(define db-file (xdg-path "XDG_DATA_HOME" "/.local/share" "/newsboat/cache.db"))

(define (error-message message . args)
  (with-output-to-port (current-error-port)
    (lambda () (apply print message args)))
  (exit 1))

(define (table-columns db table)
  (query fetch-column
         (sql db "SELECT name FROM pragma_table_info(?);") table))

(define (schema-check db)
  (let ((rss-feed-cols (table-columns db "rss_feed"))
        (rss-item-cols (table-columns db "rss_item")))
    (and (lset<= string=? '("rssurl") rss-feed-cols)
         (lset<= string=? '("title" "url" "feedurl" "unread") rss-item-cols))))

(define (sanity-check)
  (when (not (file-exists? urls-file))
    (error-message "No URLs file at " urls-file))
  (when (not (file-exists? db-file))
    (error-message "No DB file at " db-file))
  (when (not (call-with-database db-file schema))
    (error-message "No schema found for DB"))
  (when (not (call-with-database db-file schema-check))
    (error-message "Unexpected schema")))

(define (add-feed! url)
  (let ((fd (file-open urls-file (+ open/wronly open/append))))
    (file-write fd (string-append url "\n"))
    (file-close fd)))

(define (reload-feeds!)
  (run (newsboat "-x" "reload") (> "/dev/null")))

(define (unread-feeds db)
  (query fetch-all
         (sql db "SELECT url, title FROM rss_item WHERE unread = 1;")))

(define (print-unread!)
  (for-each
   (lambda (row) (apply printf "~a ~a\n" row))
   (call-with-database db-file unread-feeds)))

(define (catch-up-with-all! db)
  (exec (sql db "UPDATE rss_item SET unread = 0 WHERE unread = 1;")))

(define (known-feed? db feed-url)
  (query fetch-value
         (sql db "SELECT 1 FROM rss_feed WHERE rssurl = ?;") feed-url))

(define (catch-up-with-feed! db feed-url)
  (when (not (known-feed? db feed-url))
    (error-message "Unknown feed url"))
  (exec (sql db "UPDATE rss_item SET unread = 0 WHERE unread = 1 AND feedurl = ?;") feed-url))

(define (catch-up! #!optional feed-url)
  (if (not feed-url)
      (call-with-database db-file catch-up-with-all!)
      (call-with-database db-file (cut catch-up-with-feed! <> feed-url))))

(define (db-feed-urls)
  (call-with-database db-file
    (lambda (db) (query fetch-column (sql db "SELECT rssurl FROM rss_feed;")))))

(define (url? line)
  (and-let* ((index (substring-index "http" line 0)))
    (zero? index)))

(define (file-feed-urls)
  (let* ((lines (read-lines urls-file))
         (url-lines (filter url? lines)))
    (map (lambda (line) (car (string-split line))) url-lines)))

(define ie10-user-agent "Mozilla/5.0 (compatible; MSIE 10.0; Windows NT 6.1; Trident/6.0)")
(define custom-user-agent "linux:newsboatctl:0.0.1 (wasamasa)")

(define (http-status line)
  (let ((match (irregex-search "^HTTP/[0-9](.[0-9])? ([0-9]+) " line)))
    (if match
        (string->number (irregex-match-substring match 2))
        #f)))

(define (check-feed url)
  (receive
   (in out pid)
   (process "curl" (list "-siLA" custom-user-agent url))
   (close-output-port out)
   (let ((lines (read-lines in)))
     (close-input-port in)
     (if (null? lines)
         #f
         (let ((status-codes (filter-map http-status lines)))
           (map (lambda (status)
                  (cond
                   ((< status 300) (cons 'ok status))
                   ((< status 400) (cons 'redirect status))
                   (else (cons 'error status))))
                status-codes))))))

(define (check-feeds!)
  (for-each (lambda (url)
              (map (lambda (status)
                     (let ((type (car status))
                           (code (cdr status)))
                       (case type
                         ((ok) (print code " OK: " url))
                         ((redirect) (print code " Redirect: " url))
                         ((error) (print code " Error: " url)))))
                   (check-feed url)))
            (file-feed-urls)))

(define (main)
  (sanity-check)
  (when (null? (command-line-arguments))
    (error-message "No command given"))
  (let ((command (string->symbol (car (command-line-arguments))))
        (args (cdr (command-line-arguments))))
    (case command
      ((add-feed)
       (when (null? args)
         (error-message "add-url command requires an URL argument"))
       (add-feed! (car args)))
      ((reload-feeds) (reload-feeds!))
      ((print-unread) (print-unread!))
      ((catch-up)
       (if (null? args)
           (catch-up!)
           (catch-up! (car args))))
      ((check-feeds) (check-feeds!))
      (else (error-message "Invalid command given")))))

(main)
