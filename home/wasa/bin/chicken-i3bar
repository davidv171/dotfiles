#!/usr/bin/env chicken-scheme

(use (only srfi-1 fold take drop)
     (only srfi-18 thread-sleep!)
     (only srfi-19 format-date current-date)
     (only medea write-json)
     (prefix mpd-client mpd-)
     format)

;; TODO take time delta in account for CPU and Up/Down

(define mpd-connection (mpd-connect))

(define (format-seconds seconds)
  (let* ((time (inexact->exact (round seconds)))
         (minutes (quotient time 60))
         (seconds (remainder time 60)))
    (format "~2,'0d:~2,'0d" minutes seconds)))

(define (format-track-progress elapsed duration)
  (if (and elapsed duration)
      (format "~a/~a" (format-seconds elapsed) (format-seconds duration))
      #f))

(define (mpd-state)
  (let* ((current-status (mpd-get-status mpd-connection))
         (state (alist-ref 'state current-status))
         (playing? (not (equal? 'stop state)))
         (elapsed (alist-ref 'elapsed current-status))
         (current-song (mpd-get-current-song mpd-connection))
         (artist (alist-ref 'Artist current-song))
         (album (alist-ref 'Album current-song))
         (track (alist-ref 'Track current-song))
         (title (alist-ref 'Title current-song))
         (duration (alist-ref 'Time current-song))
         (progress (format-track-progress elapsed duration)))
    `((playing? . ,playing?)
      (artist . ,artist)
      (album . ,album)
      (track . ,track)
      (title . ,title)
      (progress . ,progress))))

(define (file-lines path)
  (call-with-input-file path
    (lambda (input) (read-lines input))))

(define wifi-max-quality 70) ; something magic, driver-specific
(define (wifi-quality)
  (let ((lines (drop (file-lines "/proc/net/wireless") 2)))
    (if (>= (length lines) 1)
        (let* ((summary (string-split (car lines)))
               (quality (string->number (list-ref summary 2))))
          (inexact->exact (round (* (/ quality wifi-max-quality) 100))))
        0)))

(define download-column 1)
(define upload-column 9)
(define (internet-speed last-downloads last-uploads)
  (define (process-net-dev lines column)
    (let ((columns
           (map (lambda (line)
                  (string->number (list-ref (string-split line) column)))
                lines)))
      (fold + 0 columns)))
  ;; use the sum of eth0 and wlan0, drop lo
  (let* ((lines (take (drop (file-lines "/proc/net/dev") 2) 2))
         (downloads (process-net-dev lines download-column))
         (download (if last-downloads
                       (- downloads last-downloads)
                       0))
         (uploads (process-net-dev lines upload-column))
         (upload (if last-uploads
                     (- uploads last-uploads)
                     0)))
    (cons (cons downloads (/ download 1024))
          (cons uploads (/ upload 1024)))))

(define cpu-count 2)
(define (cpu-usage last-cpu-usage)
  (let* ((lines (file-lines "/proc/stat"))
         (summary (cdr (string-split (car lines))))
         (user (string->number (car summary)))
         (nice (string->number (cadr summary)))
         (system (string->number (list-ref summary 2)))
         (used (+ user nice system))
         (percentage (if last-cpu-usage
                         (/ (- used last-cpu-usage) cpu-count)
                         0))) ;; hack for displaying 0% at beginning
    (cons used (inexact->exact (round percentage)))))

(define (used-ram-percentage)
  (let* ((lines (file-lines "/proc/meminfo"))
         (used (string->number (cadr (string-split (list-ref lines 6)))))
         (available (string->number (cadr (string-split (car lines))))))
    (inexact->exact (round (* (/ used available) 100)))))

(define (battery-state)
  (car (file-lines "/sys/class/power_supply/BAT0/status")))
(define (battery-percentage)
  (string->number (car (file-lines "/sys/class/power_supply/BAT0/capacity"))))

(define (current-time)
  (format-date "~H:~M" (current-date)))

(define (preamble)
  (print "{\"version\":1}")
  (print "["))

(define (main)
  (preamble)
  (let loop ((battery-blinker #f)
             (last-cpu-usage #f)
             (last-downloads #f)
             (last-uploads #f))
    (let* ((mpd-state (mpd-state))
           (mpd-playing? (alist-ref 'playing? mpd-state))
           (mpd-artist (alist-ref 'artist mpd-state))
           (mpd-album (alist-ref 'album mpd-state))
           (mpd-track (alist-ref 'track mpd-state))
           (mpd-title (alist-ref 'title mpd-state))
           (mpd-progress (alist-ref 'progress mpd-state))
           (wifi (wifi-quality))
           (cpu (cpu-usage last-cpu-usage))
           (last-cpu-usage (car cpu))
           (cpu-percentage (cdr cpu))
           (download-upload (internet-speed last-downloads last-uploads))
           (download (car download-upload))
           (last-downloads (car download))
           (download-speed (cdr download))
           (upload (cdr download-upload))
           (last-uploads (car upload))
           (upload-speed (cdr upload))
           (ram (used-ram-percentage))
           (battery-state (battery-state))
           (battery-percentage (battery-percentage))
           (time (current-time))

           (foreground "#839496")
           (background "#073642")
           (highlight "#eee8d5")
           (purple "#d33682")
           (cyan "#2aa198")
           (green "#859900")
           (orange "#cb4b16")
           (blue "#268bd2")
           (red "#dc322f")
           (violet "#6c71c4")
           (yellow "#b58900"))
      (write-json
       (vector `((full_text . ,(if mpd-playing? "MPD: " "._."))
                 (color . ,purple)
                 ,@(if mpd-playing?
                       '((separator . #f) (separator_block_width . 1))
                       '((separator . #t))))
               `((full_text . ,(if mpd-playing? mpd-artist ""))
                 (color . ,foreground)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(if mpd-playing? " - [" ""))
                 (color . ,highlight)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(if mpd-playing? (or mpd-album mpd-progress) ""))
                 (color . ,foreground)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(if mpd-playing? (if mpd-album " #" "") ""))
                 (color . ,highlight)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(if mpd-playing? (if mpd-album mpd-track "") ""))
                 (color . ,foreground)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(if mpd-playing? "] - " ""))
                 (color . ,highlight)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(if mpd-playing? mpd-title ""))
                 (color . ,foreground))
               `((full_text . "WiFi: ") (color . ,cyan)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(number->string wifi))
                 (color . ,(if (<= wifi 33) red foreground))
                 (separator . #f) (separator_block_width . 1))
               `((full_text . "%") (color . ,foreground))
               `((full_text . "Down: ") (color . ,green)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(format "~,1f" download-speed))
                 (color . ,(if (>= download-speed 500) red foreground)))
               `((full_text . "Up: ") (color . ,orange)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(format "~,1f" upload-speed))
                 (color . ,(if (>= upload-speed 90) red foreground)))
               `((full_text . "CPU: ") (color . ,blue)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(number->string cpu-percentage))
                 (color . ,(if (>= cpu-percentage 50) red foreground))
                 (separator . #f) (separator_block_width . 1))
               `((full_text . "%") (color . ,foreground))
               `((full_text . "RAM: ") (color . ,red)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(number->string ram))
                 (color . ,(if (>= ram 75) red foreground))
                 (separator . #f) (separator_block_width . 1))
               `((full_text . "%") (color . ,foreground))
               `((full_text . "BAT: ") (color . ,violet)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,(number->string battery-percentage))
                 (color . ,(cond
                            ((string=? battery-state "Full") green)
                            ((<= battery-percentage 20)
                             (if battery-blinker red background))
                            ((string=? battery-state "Discharging") yellow)
                            ((string=? battery-state "Charging") cyan)
                            (else foreground)))
                 (separator . #f) (separator_block_width . 1))
               `((full_text . "%") (color . ,foreground))
               `((full_text . "Time: ") (color . ,yellow)
                 (separator . #f) (separator_block_width . 1))
               `((full_text . ,time) (color . ,foreground))))
      (print ",")
      (flush-output)
      (thread-sleep! 1)
      (loop (not battery-blinker)
            last-cpu-usage
            last-downloads
            last-uploads))))

(main)
